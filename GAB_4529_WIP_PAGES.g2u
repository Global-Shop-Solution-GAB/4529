Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
'Hook #38130
F.Intrinsic.Control.TRY
	V.Local.SCC.Declare(STRING)
	F.Intrinsic.String.Concat("GLOBAL_",V.Caller.CompanyCode,V.Local.SCC)
	F.ODBC.Connection!CON.OpenConnection(V.Local.SCC,"Master","master")
	
	v.Local.ssql.Declare(string)
	v.Local.sret.Declare(string)
	v.Local.fret.Declare(float)
	v.Local.i.Declare(long)
	v.Local.sHTMLFile.Declare(string)
	v.Local.sHTML.Declare(string)
	v.Local.sEmpNm.Declare(string)
	v.Local.fEstHr.Declare(float)
	v.Local.fActHr.Declare(float)
	v.Local.sWorkC.Declare(string)
	v.Local.sJobnm.Declare(string)
	v.Local.sSuffx.Declare(string)
	v.Local.sSeqnc.Declare(string)
	v.Local.sWDept.Declare(string)
	v.Local.sdtNme.Declare(string)
	v.Local.sfile.Declare(string)
	v.Local.stemp.Declare(string)
	V.Local.sTDay.Declare(string)
	v.Local.fIndr.Declare(float)
	v.Local.dCurrA1.Declare(Date)
	v.Local.dCurrA2.Declare(Date)
	v.Local.sCurrA2.Declare(String)
	V.Local.dCurrA.Declare(date)
	v.Local.fCurrA.Declare(float)
	v.Local.dNow.Declare(date)
	v.Local.ldiff.Declare(long)
	v.Local.lYear.Declare(long)
	v.Local.lMonth.Declare(long)
	v.Local.lDay.Declare(long)
	v.Local.lHour.Declare(long)
	v.Local.lMin.Declare(long)
	v.Local.lSec.Declare(long)
	v.Local.serror.Declare(string)
	v.Local.dRetDate.Declare(date)
	v.Local.sColour.Declare(string)
	v.Local.bExist.Declare(Boolean)
	
	
	'F.Intrinsic.String.Build("SELECT TOP 2 CAST(START_TIME AS TIME) AS START_TIME, START_DATE, EMPLOYEE, CREW_ID  FROM V_JOBS_IN_PROCESS_G WHERE JOB + '-' + SUFFIX = '{0}' AND SEQ = '{1}' AND EMPLOYEE <> '' ",V.uGlobal.uJob(V.Local.i)!Job.Trim,V.uGlobal.uJob(V.Local.i)!Seq.Trim,V.Local.sSql)
	v.Local.ssql.Set("SELECT DISTINCT UCASE(WC_DEPT) AS WC_DEPT FROM WORKCENTERS where WC_DEPT<>''")
	
	f.ODBC.Connection!CON.OpenRecordsetRW("rs",v.Local.ssql)
	f.Intrinsic.Control.DoUntil(v.ODBC.CON!rs.EOF,=,True)
		V.Local.sdtNme.Set("dt{0}")
		f.Intrinsic.String.Build(V.Local.sdtNme,v.ODBC.CON!rs.FieldVal!WC_DEPT,v.Local.sdtNme)
		f.Data.DataTable.Create(v.Local.sdtNme,true)
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"EmplName","String")
		F.Data.DataTable.AddColumn(v.Local.sdtNme,"Job","String")
		F.Data.DataTable.AddColumn(v.Local.sdtNme,"Suffix","String")
		F.Data.DataTable.AddColumn(v.Local.sdtNme,"Seq","String")
		F.Data.DataTable.AddColumn(v.Local.sdtNme,"Workcenter","String")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"Estimate","Float")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"Actual","Float")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"Indirect","Float")
		F.Data.DataTable.AddColumn(v.Local.sdtNme,"Employee","String")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"Department","String")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"WCDept","String")
		f.Data.DataTable.AddColumn(v.Local.sdtNme,"Colour","String")
		f.ODBC.CON!rs.MoveNext
	f.Intrinsic.Control.Loop
	f.ODBC.CON!rs.Close
	
	V.Local.ssql.Set("SELECT JOB,SUFFIX,SEQ,EMPLOYEE,WORKCENTER FROM JOBS_IN_PROCESS_G WHERE JOB_DESC<>'SUSPENDED' AND JOB<>'ZINDIR' and SEQ<>'996000' ")
	F.Data.DataTable.CreateFromSQL("dtActJobs","con",v.Local.ssql)
	f.Intrinsic.Control.For(v.Local.i,0,v.DataTable.dtActJobs.Rowcount--,1)
		'Set values
		v.Local.sJobnm.Set(v.DataTable.dtActJobs(v.Local.i).JOB!FieldVal)
		v.Local.sSuffx.Set(v.DataTable.dtActJobs(v.Local.i).Suffix!Fieldval)
		v.Local.sSeqnc.Set(v.DataTable.dtActJobs(v.Local.i).Seq!Fieldval)
		v.Local.sWorkC.Set(v.DataTable.dtActJobs(v.Local.i).Workcenter!FieldVal)
		
		'Get Employee Name
		v.Local.ssql.Set("SELECT NAME FROM EMPLOYEE_MSTR WHERE EMPLOYEE='{0}'")
		F.Intrinsic.String.Build(v.Local.ssql,v.DataTable.dtActJobs(v.Local.i).Employee!FieldVal,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.sEmpNm)
		
		'Get Estimate
		v.Local.ssql.Set("SELECT HOURS_ESTIMATED FROM V_JOB_OPERATIONS WHERE JOB='{0}' AND SUFFIX='{1}' AND SEQ='{2}'")
		F.Intrinsic.String.Build(v.Local.ssql,v.Local.sJobnm,v.Local.sSuffx.Trim,v.Local.sSeqnc.Trim,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.fEstHr)
	
		'Get Actual
		v.Local.ssql.Set("SELECT HOURS_ACTUAL FROM V_JOB_OPERATIONS WHERE JOB='{0}' AND SUFFIX='{1}' AND SEQ='{2}'")
		F.Intrinsic.String.Build(v.Local.ssql,v.Local.sJobnm.Trim,v.Local.sSuffx.Trim,v.Local.sSeqnc.Trim,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.fActHr)
	
		'Get Actual Start Time
		v.Local.ssql.Set("SELECT START_DATE FROM V_JOBS_IN_PROCESS_G WHERE JOB='{0}' AND SUFFIX='{1}' AND SEQ='{2}' AND EMPLOYEE='{3}' AND JOB_DESC<>'SUSPENDED'")
		F.Intrinsic.String.Build(v.Local.ssql,v.Local.sJobnm,v.Local.sSuffx.Trim,v.Local.sSeqnc.Trim,v.DataTable.dtActJobs(v.Local.i).Employee!FieldVal,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.dCurrA1)
		
		v.Local.ssql.Set("SELECT START_TIME FROM JOBS_IN_PROCESS_G WHERE JOB='{0}' AND SUFFIX='{1}' AND SEQ='{2}' AND EMPLOYEE='{3}' AND JOB_DESC<>'SUSPENDED'")
		F.Intrinsic.String.Build(v.Local.ssql,v.Local.sJobnm,v.DataTable.dtActJobs(v.Local.i).Suffix!FieldVal,v.DataTable.dtActJobs(v.Local.i).Seq!FieldVal,v.DataTable.dtActJobs(v.Local.i).Employee!FieldVal,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.sCurrA2)
		f.Intrinsic.Date.Year(v.Local.dCurrA1,v.Local.lYear)
		f.Intrinsic.Date.Month(v.Local.dCurrA1,v.Local.lMonth)
		f.Intrinsic.Date.Day(v.Local.dCurrA1,v.Local.lDay)
		f.Intrinsic.Date.ConvertTString(v.Local.sCurrA2,"HH:NN",v.Local.dCurrA2)
		f.Intrinsic.Date.Hour(v.Local.dCurrA2,v.Local.lHour)
		f.Intrinsic.Date.Minute(v.Local.dCurrA2,v.Local.lMin)
		f.Intrinsic.Date.Second(v.Local.dCurrA2,v.Local.lSec)
		f.Intrinsic.Date.DateTimeSerial(v.Local.lYear,v.Local.lMonth,v.Local.lDay,v.Local.lHour,v.Local.lMin,v.Local.lSec,v.Local.dCurrA)
		
		'It works!
		v.Local.dNow.Set(v.Ambient.Now)
		f.Intrinsic.Date.DateDiff("N",v.Local.dCurrA,v.Local.dNow,v.Local.lDiff)
		F.Intrinsic.Control.If(V.Local.ldiff,<>,0)
			F.Intrinsic.Math.Div(V.Local.ldiff,60,V.Local.ldiff)
		F.Intrinsic.Control.EndIf
		f.Intrinsic.Math.Add(v.Local.fActHr,v.Local.ldiff,v.Local.fActHr)
	
		'Red or normal
		f.Intrinsic.Control.If(v.Local.fActHr,>,v.Local.fEstHr)
			v.Local.sColour.Set("red-cells")
		f.Intrinsic.Control.Else
			v.Local.sColour.Set("")
		f.Intrinsic.Control.EndIf
		
		'Department
		v.Local.ssql.Set("SELECT WC_DEPT FROM WORKCENTERS WHERE MACHINE='{0}'")
		F.Intrinsic.String.Build(v.Local.ssql,v.DataTable.dtActJobs(v.Local.i).Workcenter!FieldVal,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.sWDept)
		
		'Indirect Labour
		f.Intrinsic.String.Format(v.Ambient.Date,"YYYYMMDD",v.Local.stday)
		v.Local.ssql.Set("select ifnull(sum(hours_worked),0) as total FROM JOB_DETAIL where CHARGE_DATE='{0}' and flag_indirect='I' AND EMPL='{1}'")
		f.Intrinsic.String.Build(v.Local.ssql,v.Local.sTDay,v.DataTable.dtActJobs(v.Local.i).Employee!Fieldval,v.Local.ssql)
		f.ODBC.Connection!CON.ExecuteAndReturn(v.Local.ssql,v.Local.fIndr)
		
		f.Intrinsic.String.Build("dt{0}",V.Local.sWDept,v.Local.sdtNme)
		F.Intrinsic.Control.If(V.Local.sdtNme.Trim,<>,"dt")
			f.Data.DataTable.AddRow(v.Local.sdtNme,"Job",v.Local.sJobnm,"Suffix",v.Local.sSuffx,"Seq",v.Local.sSeqnc,"Workcenter",v.Local.sWorkC,"EmplName",v.Local.sEmpNm.Trim,"Estimate",v.Local.fEstHr,"Actual",v.Local.fActHr,"Department",v.Local.sWDept,"Indirect",v.Local.fIndr,"Colour",v.Local.sColour)
		f.Intrinsic.Control.EndIf
		
	f.Intrinsic.Control.Next(v.Local.i)
	v.Local.i.Set(0)
	
	v.Local.ssql.Set("SELECT DISTINCT UCASE(WC_DEPT) AS WC_DEPT FROM WORKCENTERS where WC_DEPT<>''")
	f.ODBC.Connection!CON.OpenLocalRecordsetRW("rs",v.Local.ssql)
	f.Intrinsic.Control.DoUntil(v.ODBC.CON!rs.EOF,=,true)
	
		V.Local.sdtNme.Set("dt{0}")
		f.Intrinsic.String.Build(v.Local.sdtNme,v.ODBC.CON!rs.FieldVal!WC_DEPT,v.Local.sdtNme)
		f.Data.DataTable.RemoveColumn(v.Local.sdtNme,"Employee")
		f.Data.DataTable.RemoveColumn(v.Local.sdtNme,"Department")
		f.Data.DataTable.RemoveColumn(v.Local.sdtNme,"WCDept")
		f.Data.DataTable.RemoveColumn(v.Local.sdtNme,"Colour")
		f.Data.DataTable.exportHTML(v.Local.sdtNme,"","***usetable***","",true,v.Local.SRET)
		
		F.Intrinsic.String.Replace(V.Local.sRet,"<table>","<table class="TFtable" border="0"><tbody>",V.Local.sRet)
		F.Intrinsic.String.Replace(V.Local.sRet,"</table>","</tbody></table>",V.Local.sRet)
		
		'find <td> and replace with <td class="data-cell">
		F.Intrinsic.String.Build("<td class={0}data-cell{0}>",V.Ambient.DblQuote,V.Local.sTemp)
		F.Intrinsic.String.Replace(V.Local.sRet,"<td>",V.Local.sTemp,V.Local.sRet)
		
		'left and right cells on each row<td>&nbsp;</td>
		F.Intrinsic.String.Replace(V.Local.sRet,"<tr>","<tr><td class="Empty">&nbsp;</td>",V.Local.sRet)
		F.Intrinsic.String.Replace(V.Local.sRet,"</tr>","<td class="Last">&nbsp;</td></tr>",V.Local.sRet)
		
		F.Intrinsic.String.Build("{0}\GAB\GAS\GAB_4529_WIP_TEMPLATE.html",V.Caller.PluginsDir,v.Local.sFile)
		'load template
		
		F.Intrinsic.File.File2String(V.Local.sFile,V.Local.sHTML)
		
		F.Intrinsic.String.Replace(V.Local.sHTML,"#table",V.Local.sRet,V.Local.sHTML)
		F.Intrinsic.String.Build("<td rowspan="2" class="FF_hdr">{0}",V.ODBC.CON!rs.FieldVal!WC_DEPT,V.Local.sRet)
		F.Intrinsic.String.Replace(V.Local.sHTML,"<td rowspan="2" class="FF_hdr">Shop Floor Screen",V.Local.sRet,V.Local.sHTML)
		F.Intrinsic.String.Build("<title>{0}",V.ODBC.CON!rs.FieldVal!WC_DEPT,V.Local.sRet)
		F.Intrinsic.String.Replace(V.Local.sHTML,"<title>Shop Floor Screen",V.Local.sRet,V.Local.sHTML)
		F.Intrinsic.String.Build("{0}\GAB_4529_{1}_WIP_TEMPLATE.html","C:\inetpub\wwwroot",v.ODBC.CON!rs.FieldVal!WC_DEPT,V.Local.sFile)
	
		F.Intrinsic.File.String2File(V.Local.sfile,v.Local.sHTML)
	
		f.ODBC.CON!rs.MoveNext
		f.Data.DataTable.Close(v.Local.sdtNme)
	f.Intrinsic.Control.Loop
	f.ODBC.Connection!CON.Close
	F.Data.DataTable.Close("dtActJobs")
	f.Intrinsic.Control.End
F.Intrinsic.Control.CATCH
	F.Intrinsic.UI.Msgbox(V.Ambient.ErrorDescription)
	f.ODBC.Connection!CON.Close
	f.Intrinsic.Control.End
F.Intrinsic.Control.EndTRY



Program.Sub.Main.End

Program.Sub.Comments.Start
${$0$}$$}$SUPERVSR$}$5/04/2017 8:17:46 p.m.$}$False
${$5$}$2.0.0.0$}$2
${$6$}$globalshop$}$20210923083211382$}$N6KCk/5vhz5oyVlDG2B4g8eDioysVRnIfCc0oO/RIbqYJyoi/bLlZ8NGZlf2n6nxRtSiWIY/vxQhfvKra0yRHw==
Program.Sub.Comments.End